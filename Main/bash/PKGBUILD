# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=bash
pkgbver=5.0
patchver=018
pkgver="${pkgbver}.${patchver}"
pkgrel=1
pkgdesc="The Bash package contains the Bourne-Again SHell."
arch=('x86_64')
url="http://www.gnu.org/software/${pkgname}/"
groups=('core')
license=('GPL')
makedepends=('bash'
             'binutils'
             'bison'
             'coreutils'
             'gcc'
             'grep'
             'make'
             'patch'
             'sed'
             'texinfo')
depends=('glibc'
         'ncurses'
         'readline'
         'gettext')
_confargs=""
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgbver}.tar.gz"
        "http://www.linuxfromscratch.org/~dj/${pkgname}-${pkgbver}-upstream_fixes-${patchver}.patch")
options=('debug' 'strip')

prepare() {
  cd "${pkgname}-${pkgbver}"
  patch -Np1 -i "${srcdir}/${pkgname}-${pkgbver}-upstream_fixes-${patchver}.patch"
}

build() {
  cd "${pkgname}-${pkgbver}"
  ./configure --prefix=/usr                                \
              --docdir=/usr/share/doc/${pkgname}-${pkgbver} \
              --without-bash-malloc                        \
              --with-installed-readline
  make
}

check() {
  cd "${pkgname}-${pkgbver}"
  make tests
}

package() {
  # Setup for LFS /usr merge filesystem layout
  cd "${pkgname}-${pkgbver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/bin"
  mv -vf "${pkgdir}/usr/bin/bash" "${pkgdir}/bin"
}

sha256sums=('b4a80f2ac66170b2913efbfb9f2594f1f76c7b1afd11f799e22035d63077fb4d'
            '7d24bf3d02977e7767a1269b7c6d189226e640470a7e3d9d70f819f5ba03d503')
