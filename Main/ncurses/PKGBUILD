# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgbase=ncurses
pkgname=('ncurses')
pkgver=6.2
pkgrel=1
arch=('x86_64')
url="http://www.gnu.org/software/${pkgbase}/"
license=('GPL3')
groups=('core')
makedepends=('bash'
             'binutils'
             'coreutils'
             'diffutils'
             'gawk'
             'gcc'
             'grep'
             'make'
             'patch'
             'sed')
source=("https://ftp.gnu.org/gnu/${pkgbase}/${pkgbase}-${pkgver}.tar.gz")

build() {
  cd "${pkgbase}-${pkgver}"
  sed -i '/LIBTOOL_INSTALL/d' c++/Makefile.in
  ./configure --prefix=/usr           \
              --mandir=/usr/share/man \
              --with-shared           \
              --disable-stripping     \
              --without-normal        \
              --enable-pc-files       \
              --enable-widec
  make
}

package() {
  cd "${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/lib"
  mv -v "${pkgdir}"/usr/lib/libncursesw.so.6* "${pkgdir}/lib"
  ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libncursesw.so") \
          "${pkgdir}/usr/lib/libncursesw.so"
  install -vdm755 "${pkgdir}/usr/lib/pkgconfig"
  for lib in ncurses form panel menu ; do
    rm -vf "${pkgdir}/usr/lib/lib${lib}.so"
    echo "INPUT(-l${lib}w)" > "${pkgdir}/usr/lib/lib${lib}.so"
    ln -sfv "${lib}w.pc" "${pkgdir}/usr/lib/pkgconfig/${lib}.pc"
  done
  rm -vf "${pkgdir}/usr/lib/libcursesw.so"
  echo "INPUT(-lncursesw)" > "${pkgdir}/usr/lib/libcursesw.so"
  ln -sfv libncurses.so "${pkgdir}/usr/lib/libcurses.so"
  install -vdm755 "${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}"
  cp -v -R doc/* "${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}"

  make distclean
  ./configure --prefix=/usr         \
              --with-shared         \
              --without-normal      \
              --disable-stripping   \
              --without-cxx-binding \
              --with-abi-version=5 
  make sources libs
  cp -av lib/lib*.so.5* "${pkgdir}/usr/lib"
}

sha256sums=('30306e0c76e0f9f1f0de987cf1c82a5c21e1ce6568b9227f7da5b71cbea86c9d')
