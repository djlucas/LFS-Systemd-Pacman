# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=libtirpc
pkgver=1.2.6
pkgrel=1
pkgdesc="The libtirpc package contains libraries that support programs that use the Remote Procedure Call (RPC) API."
arch=('x86_64')
url="http://git.linux-nfs.org/?p=steved/libtirpc.git;a=summary"
license=('BSD')
depends=('rpcsvc-proto')
backup=('etc/netconfig')
source=(https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
options=('debug' 'strip')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr    \
              --sysconf=/etc   \
              --disable-static \
              --disable-gssapi
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/lib"
  mv -v "${pkgdir}"/usr/lib/libtirpc.so.* "${pkgdir}/lib"
  ln -sfv ../../lib/libtirpc.so.3.0.0 "${pkgdir}/usr/lib/libtirpc.so"
}
sha256sums=('4278e9a5181d5af9cd7885322fdecebc444f9a3da87c526e7d47f7a12a37d1cc')
