# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=perl
pkgver=5.32.0
pkgverd=5.32
pkgrel=1
pkgdesc="The Perl package contains the Practical Extraction and Report Language." 
arch=('x86_64')
url="http://www.perl.org/"
license=('GPL3')
groups=('core')
depends=('glibc')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gcc'
             'gettext'
             'grep'
             'make'
             'perl'
             'texinfo')
source=("http://www.cpan.org/src/5.0/${pkgname}-${pkgver}.tar.xz")        
options=('debug' 'strip')

build(){
  cd "${pkgname}-${pkgver}"
  export BUILD_ZLIB=False
  export BUILD_BZIP2=0
  sh Configure -des -Dprefix=/usr                 \
               -Dvendorprefix=/usr                          \
               -Dprivlib=/usr/lib/perl5/${pkgverd}/core_perl      \
               -Darchlib=/usr/lib/perl5/${pkgverd}/core_perl      \
               -Dsitelib=/usr/lib/perl5/${pkgverd}/site_perl      \
               -Dsitearch=/usr/lib/perl5/${pkgverd}/site_perl     \
               -Dvendorlib=/usr/lib/perl5/${pkgverd}/vendor_perl  \
               -Dvendorarch=/usr/lib/perl5/${pkgverd}/vendor_perl \
               -Dman1dir=/usr/share/man/man1                \
               -Dman3dir=/usr/share/man/man3                \
               -Dpager="/usr/bin/less -isR"                 \
               -Duseshrplib                                 \
               -Dusethreads
make
}

check(){
  cd "${pkgname}-${pkgver}"
  make -k check || true
}

package(){
  cd "${pkgname}-${pkgver}"
  export BUILD_ZLIB=False
  export BUILD_BZIP2=0
  make DESTDIR="${pkgdir}" install
}
sha256sums=('6f436b447cf56d22464f980fac1916e707a040e96d52172984c5d184c09b859b')
