# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=util-linux
pkgver=2.36
pkgrel=1
pkgdesc="The Util-linux package contains miscellaneous utility programs. Among them are utilities for handling file systems, consoles, partitions, and messages." 
arch=('x86_64')
url="http://freecode.com/projects/util-linux"
license=('GPL3')
groups=('core')
depends=('gcc'
         'glibc')
makedepends=('bash'
             'binutils'
             'coreutils'
             'diffutils'
             'eudev-data'
             'findutils'
             'gawk'
             'gettext'
             'grep'
             'make'
             'ncurses'
             'sed'
             'zlib')
source=("https://www.kernel.org/pub/linux/utils/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
otpions=('emptydirs')

build(){
  cd "${pkgname}-${pkgver}"
  ./configure ADJTIME_PATH=/var/lib/hwclock/adjtime   \
              --docdir="/usr/share/doc/${pkgname}-${pkgver}" \
              --disable-chfn-chsh  \
              --disable-login      \
              --disable-nologin    \
              --disable-su         \
              --disable-setpriv    \
              --disable-runuser    \
              --disable-pylibmount \
              --disable-static     \
              --without-python     \
              --without-systemd    \
              --without-systemdsystemunitdir
  make
}

check(){
  cd "${pkgname}-${pkgver}"
  make -k check || true
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/var/lib/hwclock"
}

sha256sums=('9e4b1c67eb13b9b67feb32ae1dc0d50e08ce9e5d82e1cccd0ee771ad2fa9e0b1')
