# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

_pkgname=libXvMC
pkgname=xorg-libXvMC
pkgver=1.0.12
pkgrel=1
pkgdesc="libXvMC contains the X.org video motion compensation library."
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
groups=('xorg')
depends=('glibc'
         'libxcb'
         'xorg-libX11'
         'xorg-libXau'
         'xorg-libXdmcp'
         'xorg-libXext'
         'xorg-libXv')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gawk'
             'gcc'
             'gettext'
             'make'
             'xorg-util-macros'
             'xorg-xorgproto')
optdepends=('fop'
            'libxslt'
            'links'
            'lynx'
            'w3m'
            'xmlto')
source=("${url}/releases/individual/lib/${_pkgname}-${pkgver}.tar.bz2")

build(){
  cd "${_pkgname}-${pkgver}"
  ./configure --prefix=/usr        \
              --sysconfdir=/etc    \
              --localstatedir=/var \
              --disable-static     \
              --docdir="/usr/share/doc/${_pkgname}-${pkgver}"
  make
}

package(){
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/include/X11/extensions/vldXvMC.h"
}

sha256sums=('6b3da7977b3f7eaf4f0ac6470ab1e562298d82c4e79077765787963ab7966dcd')
