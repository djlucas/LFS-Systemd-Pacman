# Maintainer: DJ Lucas <dj@linuxfromscratch@org>

pkgname=dbus
pkgver=1.12.20
pkgrel=2
pkgdesc="D-Bus supplies a message bus for IPC, both a system daemon and a per-user-login-session daemon."
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/${pkgname}"
groups=('core')
license=('GPL')
makedepends=('bash'
             'binutils'
             'coreutils'
             'diffutils'
             'gawk'
             'gcc'
             'grep'
             'make'
             'sed'
             'util-linux')
depends=('glibc'
         $(pacman -Q elogind >/dev/null 2>&1 && echo 'elogind')
         $(pacman -Q libx11 >/dev/null 2>&1 && echo 'libx11'))
optdepends=('doxygen'
            'xmlto'
            'ducktype'
            'yelp-tools'
            'dbus-glib'
            'python2-dbus-python'
            'python3-pygobject'
            'valgrind'
            $(pacman -Q elogind >/dev/null 2>&1 || echo 'elogind')
            $(pacman -Q libx11 >/dev/null 2>&1 || echo 'libx11'))
source=("http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "dbus.init")
install=dbus.install
_confargs=""

build(){
  cd ${srcdir}/${pkgname}-${pkgver}

  # Handle optional deps
  if [ ! $(pacman -Q doxygen > /dev/null 2>&1) ]; then
    _confargs="${_confargs} --disable-doxygen-docs"
  fi
  if [ ! $(pacman -Q xmlto > /dev/null 2>&1) ]; then
    _confargs="${_confargs} --disable-xml-docs"
  fi

  ./configure --prefix=/usr                                   \
              --sysconfdir=/etc                               \
              --localstatedir=/var                            \
              --enable-user-session                           \
              --disable-static                                \
              --with-systemduserunitdir=no                    \
              --with-systemdsystemunitdir=no                  \
              ${_confargs}                                    \
              --docdir=/usr/share/doc/"${pkgname}-${pkgver}"  \
              --with-console-auth-dir=/run/console            \
              --with-system-pid-file=/run/dbus/pid            \
              --with-system-socket=/run/dbus/system_bus_socket
  make
}

check(){
  cd "${pkgname}-${pkgver}"
  make check
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/var/run"
  install -vdm755 "${pkgdir}/lib"
  mv -v "${pkgdir}"/usr/lib/libdbus-1.so.* "${pkgdir}/lib"
  ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libdbus-1.so") \
          "${pkgdir}/usr/lib/libdbus-1.so"
  chown -v root:messagebus "${pkgdir}/usr/libexec/dbus-daemon-launch-helper"
  chmod -v 4750 "${pkgdir}/usr/libexec/dbus-daemon-launch-helper"
  ln -sfv /var/lib/dbus/machine-id "${pkgdir}/etc/"
  install -vdm755 "${pkgdir}/etc/rc.d/init.d"
  install -vm750 "${srcdir}/dbus.init" "${pkgdir}/etc/rc.d/init.d/dbus"
}

sha256sums=('f77620140ecb4cdc67f37fb444f8a6bea70b5b6461f12f1cbe2cec60fa7de5fe'
            '505f0fd6e03918ea19febab8338d0e95e98c4f638f5ecaf2544f37a168c6d272')
