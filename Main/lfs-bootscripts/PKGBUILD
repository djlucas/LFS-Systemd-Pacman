# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=lfs-bootscripts
pkgver=20201019
pkgrel=1
pkgdesc="The Sysvinit package contains programs for controlling the startup, running, and shutdown of the system."
arch=('x86_64')
url="http://www.linuxfromscratch.org"
license=('GPL3')
groups=('core')
depends=('glibc'
         'sysvinit')
makedepends=('coreutils'
             'make'
             'grep'
             'sed')
optdepends=('python3-lsb-tools')
backup=('etc/inittab'
        'etc/sysconfig/clock'
        'etc/sysconfig/console'
        'etc/sysconfig/createfiles'
        'etc/sysconfig/modules'
        'etc/sysconfig/rc.site'
        'etc/sysconfig/udev_retry')
source=("http://www.linuxfromscratch.org/lfs/downloads/development/${pkgname}-${pkgver}.tar.xz")
options=('emptydirs')
install=lfs-bootscripts.install

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}"/etc/rc.d/rc?.d/*
  cat > "${pkgdir}/etc/inittab" << "EOF"
# Begin /etc/inittab

id:3:initdefault:

si::sysinit:/etc/rc.d/init.d/rc S

l0:0:wait:/etc/rc.d/init.d/rc 0
l1:S1:wait:/etc/rc.d/init.d/rc 1
l2:2:wait:/etc/rc.d/init.d/rc 2
l3:3:wait:/etc/rc.d/init.d/rc 3
l4:4:wait:/etc/rc.d/init.d/rc 4
l5:5:wait:/etc/rc.d/init.d/rc 5
l6:6:wait:/etc/rc.d/init.d/rc 6

ca:12345:ctrlaltdel:/sbin/shutdown -t1 -a -r now

su:S016:once:/sbin/sulogin

1:2345:respawn:/sbin/agetty --noclear tty1 9600
2:2345:respawn:/sbin/agetty tty2 9600
3:2345:respawn:/sbin/agetty tty3 9600
4:2345:respawn:/sbin/agetty tty4 9600
5:2345:respawn:/sbin/agetty tty5 9600
6:2345:respawn:/sbin/agetty tty6 9600

# End /etc/inittab
EOF
  cat > "${pkgdir}/etc/sysconfig/clock" << "EOF"
# Begin /etc/sysconfig/clock

UTC=1

# Set this to any options you might need to give to hwclock,
# such as machine hardware clock type for Alphas.
CLOCKPARAMS=

# End /etc/sysconfig/clock
EOF
}

sha256sums=('58bfe996e0d08d0bc3af31d7b2d3a7af8fcbe2249c41e312ba7af25d37c2d1a6')
