# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=libnsl
pkgver=1.3.0
pkgrel=1
pkgdesc="The libnsl package contains the public client interface for NIS(YP) and NIS+."
arch=(x86_64)
url='https://github.com/thkukuk/libnsl'
license=('LGPL2.1')
depends=(libtirpc)
source=("https://github.com/thkukuk/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
options=('debug' 'strip')

build() {
  cd "${pkgname}-${pkgver}"
  autoreconf -fi
  ./configure --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/lib"
  mv -v "${pkgdir}"/usr/lib/libnsl.so.2* "${pkgdir}/lib"
  ln -sfv ../../lib/libnsl.so.2.0.1 "${pkgdir}/usr/lib/libnsl.so"
}
sha256sums=('8e88017f01dd428f50386186b0cd82ad06c9b2a47f9c5ea6b3023fc6e08a6b0f')
