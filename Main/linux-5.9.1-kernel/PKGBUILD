# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

# Use pkgver as part of pkgname so that multiple kernels can co-exist
# In LFS-Pacman, the grub hook is installed with grub
_pkgname=linux
_pkgver=5.9.1
pkgname="linux-${_pkgver}-kernel"
pkgver=1
pkgrel=1
pkgdesc='Linux kernel for LFS-Pacman'
url="https://www.kernel.org"
arch=('x86_64')
license=('GPL2')
makedepends=('bash'
             'bc'
             'binutils'
             'coreutils'
             'cpio'
             'diffutils'
             'findutils'
             'gcc'
             'glibc'
             'grep'
             'gzip'
             'kmod'
             'libelf'
             'make'
             'ncurses'
             'openssl'
             'perl'
             'sed')
options=('!strip')
source=("https://www.kernel.org/pub/${_pkgname}/kernel/v5.x/${_pkgname}-${_pkgver}.tar.xz"
        "kernel-config")

prepare() {
  cd "${_pkgname}-${_pkgver}"
  make mrproper
  cp "${srcdir}/kernel-config" .config
  make olddefconfig
}

build() {
  cd "${_pkgname}-${_pkgver}"
  make
  make modules
}

package() {
  cd "${_pkgname}-${_pkgver}"
  make INSTALL_MOD_PATH="${pkgdir}" modules_install
  install -vdm755 "${pkgdir}/boot"
  install -vm644 arch/x86/boot/bzImage \
                 "${pkgdir}/boot/vmlinuz-${_pkgver}-lfs-${pkgrel}"
  install -vm644 System.map "${pkgdir}/boot/System.map-${_pkgver}-lfs-${pkgrel}"
  install -vm644 .config "${pkgdir}/boot/config-${_pkgver}-lfs-${pkgrel}"
  _distver="${_pkgver}-lfs-${pkgrel}"
  #install -vdm755 "${pkgdir}/etc/mkinitcpio.d/"
  #sed "s@\%PKGBASE\%@${_distver}@g" /usr/share/mkinitcpio/hook.preset > \
  #                                "${pkgdir}/etc/mkinitcpio.d/${_pkgver}.preset"
}

# vim:set ts=8 sts=2 sw=2 et:
sha256sums=('ba4a11e93896305835c630969cf330ae808b0e43f09b375b510cde1bd0efc036'
            '34a6b807b16a58f1f586e02575e8e62d912b5b5f1601b0147c3fb462adbb4166')
