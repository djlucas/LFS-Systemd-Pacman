# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

_pkgname=xrandr
pkgname=xorg-xrandr
pkgver=1.5.1
pkgrel=1
pkgdesc="xrandr is a primitive command line interface to RandR extension."
arch=('x86_64')
url="https://xorg.freedesktop.org"
license=('custom')
groups=('xorg')
depends=('glibc'
         'libxcb'
         'xorg-libX11'
         'xorg-libXau'
         'xorg-libXdmcp'
         'xorg-libXext'
         'xorg-libXrandr'
         'xorg-libXrender')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gawk'
             'gcc'
             'gettext'
             'make'
             'xorg-util-macros'
             'xorg-xorgproto')
source=("${url}/releases/individual/app/${_pkgname}-${pkgver}.tar.xz")

build(){
  cd "${_pkgname}-${pkgver}"
  ./configure --prefix=/usr        \
              --sysconfdir=/etc    \
              --localstatedir=/var
  make
}

package(){
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/bin/xkeystone"
}

sha256sums=('7bc76daf9d72f8aff885efad04ce06b90488a1a169d118dea8a2b661832e8762')
