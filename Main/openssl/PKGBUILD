# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=openssl
_ver=1.1.1g
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgrel=1
pkgdesc="The OpenSSL package contains management tools and libraries relating to cryptography. These are useful for providing cryptographic functions to other packages, such as OpenSSH, email applications, and web browsers (for accessing HTTPS sites)."
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
groups=('core')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gcc'
             'glibc'
             'make'
             'sed')
depends=('zlib'
         'perl')
optdepends=('ca-certificates: provides certificate authority store')
backup=('etc/ssl/openssl.cnf')
source=("https://www.${pkgname}.org/source/${pkgname}-${_ver}.tar.gz")

build() {
  cd "${pkgname}-${_ver}"
  ./config --prefix=/usr         \
           --openssldir=/etc/ssl \
           --libdir=lib          \
           shared                \
           zlib-dynamic
  make
}

check() {
  cd "${pkgname}-${_ver}"
  make test
}

package() {
  cd "${pkgname}-${_ver}"
  sed -i '/INSTALL_LIBS/s/libcrypto.a libssl.a//' Makefile
  make DESTDIR="${pkgdir}" MANSUFFIX=ssl install
  mv -v "${pkgdir}/usr/share/doc/${pkgname}" \
        "${pkgdir}/usr/share/doc/${pkgname}-${_ver}"
  cp -vfr doc/* "${pkgdir}/usr/share/doc/${pkgname}-${_ver}"
}

sha256sums=('ddb04774f1e32f0c49751e21b67216ac87852ceb056b75209af2443400636d46')

