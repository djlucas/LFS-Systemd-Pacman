# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=grep
pkgver=3.5
pkgrel=1
pkgdesc="The Grep package contains programs for searching through files." 
arch=('x86_64')
url="http://www.gnu.org/software/grep"
license=('GPL3')
groups=('core')
depends=('glibc')
makedepends=('bash'
             'binutils'
             'coreutils'
             'diffutils'
             'gcc'
             'gettext'
             'grep'
             'make'
             'patch'
             'sed'
             'texinfo')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz")

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr  --bindir=/bin
  make
}

check(){
  cd "${pkgname}-${pkgver}"
  make check
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

sha256sums=('b82ac77707c2ab945520c8404c9fa9f890f7791a62cf2103cf6238acad87a44a')
