# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=nss
pkgver=3.57
pkgdesc="The Network Security Services (NSS) package is a set of libraries designed to support cross-platform development of security-enabled client and server applications."
pkgrel=1
arch=('x86_64')
url="http://www.mozila.org/"
license=('LGPL3')
groups=('core')
source=("https://archive.mozilla.org/pub/security/${pkgname}/releases/NSS_3_57_RTM/src/${pkgname}-${pkgver}.tar.gz"
        "http://www.linuxfromscratch.org/patches/blfs/svn/${pkgname}-${pkgver}-standalone-1.patch")
depends=('glibc'
         'nspr'
         'sqlite'
         'p11-kit')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gcc'
             'grep'
             'make'
             'sed')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/${pkgname}-${pkgver}-standalone-1.patch"
}

build() {
  cd "${pkgname}-${pkgver}/nss"
  make \
    NSPR_INCLUDE_DIR=/usr/include/nspr \
    USE_SYSTEM_ZLIB=1 \
    ZLIB_LIBS=-lz \
    NSS_ENABLE_WERROR=0 \
    USE_64=1 \
    NSS_USE_SYSTEM_SQLITE=1
}

check(){
  cd "${pkgname}-${pkgver}/nss/tests"
  HOST=localhost DOMSUF=localdomain ./all.sh
}

package() {
  cd "${pkgname}-${pkgver}/dist"
  install -vdm755 "${pkgdir}/usr/lib"
  install -vm755 Linux*/lib/*.so "${pkgdir}/usr/lib"
  install -vm644 Linux*/lib/{*.chk,libcrmf.a} "${pkgdir}/usr/lib"
  install -vdm755 "${pkgdir}/usr/include/nss"
  cp -vRL {public,private}/nss/* "${pkgdir}/usr/include/nss"
  chmod -v 644 "${pkgdir}"/usr/include/nss/*
  install -vdm755 "${pkgdir}/usr/bin"
  install -vm755 Linux*/bin/{certutil,nss-config,pk12util} "${pkgdir}/usr/bin"
  install -vdm755 "${pkgdir}/usr/lib/pkgconfig"
  install -vm644 Linux*/lib/pkgconfig/nss.pc "${pkgdir}/usr/lib/pkgconfig"
  # Use libnssckbi.so from p11-kit
  rm -fv "${pkgdir}/usr/lib/libnssckbi.so"
}

sha256sums=('55a86c01be860381d64bb4e5b94eb198df9b0f098a8af0e58c014df398bdc382'
            '87bb1af0b11fd41311b9899187f6e4b3fca9940651123c7bc836ec7497d2da84')
