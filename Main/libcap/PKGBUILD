# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=libcap
pkgver=2.45
pkgrel=1
pkgdesc="The Libcap package implements the user-space interfaces to the POSIX 1003.1e capabilities available in Linux kernels."
arch=('x86_64')
url="https://sites.google.com/site/fullycapable/"
license=('GPL3')
groups=('core')
source=("https://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/${pkgname}-${pkgver}.tar.xz")
depends=('glibc' 'linux-pam')
makedepends=('attr'
             'bash'
             'binutils'
             'coreutils'
             'gcc'
             'make'
             'perl'
             'sed')
backup=('etc/security/capability.conf')

prepare() {
  cd "${pkgname}-${pkgver}"
  # use our buildflags - thanks Arch
  sed -i "s/CFLAGS :=/CFLAGS += \$(CPPFLAGS) /" Make.Rules
  sed -i "s/LDFLAGS :=/LDFLAGS +=/" Make.Rules
}

build() {
  cd "${pkgname}-${pkgver}"
  make lib=lib
  make -C pam_cap
}

package() {
  cd "${pkgname}-${pkgver}"
  install -vdm755 "${pkgdir}/usr/lib/pkgconfig"
  make lib=lib PKGCONFIGDIR="/usr/lib/pkgconfig" FAKEROOT="${pkgdir}" install
  install -vdm755 "${pkgdir}/usr/lib"
  for libname in cap psx; do
      chmod -v 755 "${pkgdir}/lib/lib${libname}.so.2.45"
      rm -v "${pkgdir}/lib/lib${libname}.so"
      ln -sfv ../../lib/lib${libname}.so.2 "${pkgdir}/usr/lib/lib${libname}.so"
  done
  install -vdm755 "${pkgdir}"/{etc,lib}/security
  install -v -m755 pam_cap/pam_cap.so "${pkgdir}/lib/security"
  install -v -m644 pam_cap/capability.conf "${pkgdir}/etc/security"
}

sha256sums=('d66639f765c0e10557666b00f519caf0bd07a95f867dddaee131cd284fac3286')
