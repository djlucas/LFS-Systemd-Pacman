# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=libcap
pkgver=2.44
pkgrel=1
pkgdesc="The Libcap package implements the user-space interfaces to the POSIX 1003.1e capabilities available in Linux kernels."
arch=('x86_64')
url="https://sites.google.com/site/fullycapable/"
license=('GPL3')
groups=('core')
source=("https://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/${pkgname}-${pkgver}.tar.xz")
depends=('glibc' 'linux-pam')
makedepends=('attr'
             'bash'
             'binutils'
             'coreutils'
             'gcc'
             'make'
             'perl'
             'sed')
backup=('etc/security/capability.conf')

prepare() {
  cd "${pkgname}-${pkgver}"
  # use our buildflags - thanks Arch
  sed -i "s/CFLAGS :=/CFLAGS += \$(CPPFLAGS) /" Make.Rules
  sed -i "s/LDFLAGS :=/LDFLAGS +=/" Make.Rules
}

build() {
  cd "${pkgname}-${pkgver}"
  make -C libcap lib=lib PKGCONFIGDIR=/usr/lib/pkgconfig
  make -C progs
  make -C doc
  make -C pam_cap
}

package() {
  cd "${pkgname}-${pkgver}"
  make -C libcap lib=lib PKGCONFIGDIR="${pkgdir}"/usr/lib/pkgconfig \
          prefix="${pkgdir}/usr" install-shared-cap
  chmod -v 755 "${pkgdir}/usr/lib/libcap.so"
  make -C progs prefix="${pkgdir}/usr" install
  make -C doc   prefix="${pkgdir}/usr" install
  install -vdm755 "${pkgdir}"/{etc,lib}/security
  install -v -m755 pam_cap/pam_cap.so "${pkgdir}/lib/security"
  install -v -m644 pam_cap/capability.conf "${pkgdir}/etc/security"
}

sha256sums=('92188359cd5be86e8e5bd3f6483ac6ce582264f912398937ef763def2205c8e1')
