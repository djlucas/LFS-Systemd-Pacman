# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=fakeroot
pkgver=1.24
pkgrel=1
pkgdesc="The fakeroot package provides a tool for simulating superuser privileges."
arch=('x86_64')
url="https://tracker.debian.org/pkg/fakeroot"
license=('GPL')
groups=('core')
depends=('bash'
         'gcc'
         'glibc'
         'sed'
         'util-linux')
makedepends=('autoconf'
             'automake'
             'm4'
             'make'
             'patch')
#checkdepends=('sharutils')
source=("http://ftp.debian.org/debian/pool/main/f/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz"
        "${pkgname}-${pkgver}-silence-dlerror-1.patch::https://raw.githubusercontent.com/archlinux/svntogit-packages/packages/fakeroot/trunk/silence-dlerror.patch")

prepare(){
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/${pkgname}-${pkgver}-silence-dlerror-1.patch"
}

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr  \
              --disable-static \
              --with-ipc=sysv
  make
}

#check(){
#  cd "${pkgname}-${pkgver}"
#  make check
#}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
 }

sha256sums=('2e045b3160370b8ab4d44d1f8d267e5d1d555f1bb522d650e7167b09477266ed'
            '347f6496c93ed0d91dcb554db92602b50520a16308e3b8bc33822bd69966267b')
