# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=graphite2
pkgver=1.3.14
pkgrel=1
pkgdesc="Graphite2 is a rendering engine for graphite fonts."
arch=('x86_64')
url="http://github.com/silnrsi/graphite"
license=('GPL3')
depends=('glibc'
         'freetype')
makedepends=('bash'
             'binutils'
             'cmake'
             'coreutils'
             'gcc')
optdepends=('asciidoc'
            'dblatex'
            'doxygen'
            'tex')
checkdepends=('python3-fonttools')
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tgz")

prepare(){
  cd "${pkgname}-${pkgver}"
  sed -i '/cmptest/d' tests/CMakeLists.txt
  mkdir build
}

build(){
  cd "${pkgname}-${pkgver}/build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
  if $(pacman -Qs asciidoc doxygen tex > /dev/null 2>&1); then
    make docs
  fi
}

## cmp tests will fail if python3-fonttools is not installed
check(){
  cd "${pkgname}-${pkgver}/build"
  if $(pacman -Qs python3-fonttools > /dev/null 2>&1); then
    make test
  fi
}

package(){
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  if $(pacman -Qs asciidoc doxygen tex > /dev/null 2>&1); then
    install -vdm755 "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
    cp -vf doc/{GTF,manual}.html \
           "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
    cp -vf doc/{GTF,manual}.pdf \
           "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  fi
}

sha256sums=('f99d1c13aa5fa296898a181dff9b82fb25f6cc0933dbaa7a475d8109bd54209d')
