# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>
pkgname=linux-pam
pkgver=1.4.0
pkgrel=2
pkgdesc="The Linux PAM package contains Pluggable Authentication Modules used to enable the local system administrator to choose how applications authenticate users."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/${pkgname}/"
license=('GPL')
groups=('core')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gcc'
             'glibc'
             'make')
depends=('glibc'
         $( pacman -Q db >/dev/null 2>&1 && echo 'db' )
         $( pacman -Q cracklib >/dev/null 2>&1 && echo 'cracklib' )
         $( pacman -Q libtirpc >/dev/null 2>&1 && echo 'libtirpc' ))
optdepends=('db: Use Berkely DB for backend'
            'cracklib: Check for quality passwords'
            'libtirpc: use NFS RPC')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/v${pkgver}/Linux-PAM-${pkgver}.tar.xz"
        "https://github.com/${pkgname}/${pkgname}/releases/download/v${pkgver}/Linux-PAM-${pkgver}-docs.tar.xz")
noextract=("Linux-PAM-${pkgver}-docs.tar.xz")
backup=('etc/pam.d/system-account'
        'etc/pam.d/system-auth'
        'etc/pam.d/system-session'
        'etc/pam.d/system-password'
        'etc/pam.d/other')

prepare(){
  cd "Linux-PAM-${pkgver}"
  tar -xf "../Linux-PAM-${pkgver}-docs.tar.xz" --strip-components=1
  # Don't install systemd specific items on SysV
  sed -e /service_DATA/d \
      -i modules/pam_namespace/Makefile.am
  autoreconf
}

build(){
  cd "Linux-PAM-${pkgver}"
  ./configure --prefix=/usr                    \
              --sysconfdir=/etc                \
              --libdir=/usr/lib                \
              --disable-regenerate-docu        \
              --enable-securedir=/lib/security \
              --docdir="/usr/share/doc/Linux-PAM-${pkgver}"
  make
}

package(){
  cd "Linux-PAM-${pkgver}"
  make DESTDIR="${pkgdir}" install
  chmod -v 4755 "${pkgdir}/sbin/unix_chkpwd"
  install -vdm755 "${pkgdir}/lib"
  for file in pam pam_misc pamc
  do
    mv -v "${pkgdir}/usr/lib/lib${file}".so.* "${pkgdir}/lib"
    ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/lib${file}.so") \
            "${pkgdir}/usr/lib/lib${file}.so"
  done

# /etc/pam.d files
  install -vdm755 "${pkgdir}/etc/pam.d"
  cat > "${pkgdir}/etc/pam.d/system-account" << "EOF"
# Begin /etc/pam.d/system-account

account   required    pam_unix.so

# End /etc/pam.d/system-account
EOF

  cat > "${pkgdir}/etc/pam.d/system-auth" << "EOF"
# Begin /etc/pam.d/system-auth

auth      required    pam_unix.so

# End /etc/pam.d/system-auth
EOF

  cat > "${pkgdir}/etc/pam.d/system-session" << "EOF"
# Begin /etc/pam.d/system-session

session   required    pam_unix.so

# End /etc/pam.d/system-session
EOF

  cat > "${pkgdir}/etc/pam.d/system-password" << "EOF"
# Begin /etc/pam.d/system-password

# check new passwords for strength (man pam_cracklib)
password  required    pam_cracklib.so   type=Linux retry=3 difok=5 \
                                        difignore=23 minlen=9 dcredit=1 \
                                        ucredit=1 lcredit=1 ocredit=1 \
                                        dictpath=/lib/cracklib/pw_dict
# use sha512 hash for encryption, use shadow, and use the
# authentication token (chosen password) set by pam_cracklib
# above (or any previous modules)
password  required    pam_unix.so       sha512 shadow use_authtok

# End /etc/pam.d/system-password
EOF

  cat > "${pkgdir}/etc/pam.d/other" << "EOF"
# Begin /etc/pam.d/other

auth        required        pam_warn.so
auth        required        pam_deny.so
account     required        pam_warn.so
account     required        pam_deny.so
password    required        pam_warn.so
password    required        pam_deny.so
session     required        pam_warn.so
session     required        pam_deny.so

# End /etc/pam.d/other
EOF

}

sha256sums=('cd6d928c51e64139be3bdb38692c68183a509b83d4f2c221024ccd4bcddfd034'
            '351764a0643052564a4b840320744c7e402112a2a57d2ac04511a6d22dc52e04')
