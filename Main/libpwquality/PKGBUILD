# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=libpwquality
pkgver=1.4.2
pkgrel=1
pkgdesc="The libpwquality package provides common functions for password quality checking and also scoring them based on their apparent randomness."
arch=('x86_64')
url="https://github.com/libpwquality/libpwquality"
license=('GPL')
depends=('cracklib'
         'linux-pam')
makedepends=('python')
backup=('etc/security/pwquality.conf')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2")
options=('debug' 'strip')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr                  \
              --disable-static               \
              --with-securedir=/lib/security \
              --with-python-binary=python3
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}"/etc/security/pwquality.conf.d
  mv -v "${pkgdir}"/usr/lib/libpwquality.so.* "${pkgdir}/lib"
  ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libpwquality.so") \
          "${pkgdir}/usr/lib/libpwquality.so"
  install -vdm755 "${pkgdir}/etc/pam.d"
  cat > "${pkgdir}/etc/pam.d/system-password.libpwquality" << "EOF"
# Begin /etc/pam.d/system-password

# check new passwords for strength (man pam_pwquality)
password  required    pam_pwquality.so   authtok_type=UNIX retry=1 difok=1 \
                                         minlen=8 dcredit=0 ucredit=0 \
                                         lcredit=0 ocredit=0 minclass=1 \
                                         maxrepeat=0 maxsequence=0 \
                                         maxclassrepeat=0 geoscheck=0 \
                                         dictcheck=1 usercheck=1 \
                                         enforcing=1 badwords="" \
                                         dictpath=/lib/cracklib/pw_dict
# use sha512 hash for encryption, use shadow, and use the
# authentication token (chosen password) set by pam_pwquality
# above (or any previous modules)
password  required    pam_unix.so        sha512 shadow use_authtok

# End /etc/pam.d/system-password
EOF
}

sha256sums=('5263e09ee62269c092f790ac159112aed3e66826a795e3afec85fdeac4281c8e')
