# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=elogind
pkgver=243.7
pkgrel=2
arch=('x86_64')
url="https://github.com/${pkgname}/${pkgname}/"
license=('GPL3')
groups=('core')
depends=('glibc'
         'dbus'
         'polkit'
         'linux-pam')
optdepends=('docbook-xml'
            'docbook-xsl'
            'libxslt'
            'lxml'
            'gobject-introspection'
            'zsh'
            'valgrind')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gcc'
             'gettext'
             'grep'
             'make'
             'meson'
             'ninja'
             'sed'
             'texinfo')
# Pamcan's src copy is just broken with this one, just don't strip it.
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
install=elogind.install

prepare(){
  cd "${pkgname}-${pkgver}"
  mkdir build
  cd build
  meson --prefix=/usr                        \
        --sysconfdir=/etc                    \
        --localstatedir=/var                 \
        -Dcgroup-controller=elogind          \
        -Ddbuspolicydir=/etc/dbus-1/system.d \
        -Dman=auto                           \
        ..
}

build(){
  cd "${pkgname}-${pkgver}/build"
  ninja
}

package(){
  cd "${pkgname}-${pkgver}/build"
  DESTDIR="${pkgdir}" ninja install
  ln -sfv  libelogind.pc "${pkgdir}/usr/lib/pkgconfig/libsystemd.pc"
  ln -sfvn elogind "${pkgdir}/usr/include/systemd"

  install -vdm755 "${pkgdir}/etc/pam.d"
  cat > "${pkgdir}/etc/pam.d/elogind-session" << "EOF"
# Begin /etc/pam.d/elogind-session

session  required    pam_loginuid.so
session  optional    pam_elogind.so

# End /etc/pam.d/elogind-session
EOF
  cat > "${pkgdir}/etc/pam.d/elogind-user" << "EOF"
# Begin /etc/pam.d/elogind-user

account  required    pam_access.so
account  include     system-account

session  required    pam_env.so
session  required    pam_limits.so
session  required    pam_unix.so
session  required    pam_loginuid.so
session  optional    pam_keyinit.so force revoke
session  optional    pam_elogind.so

auth     required    pam_deny.so
password required    pam_deny.so

# End /etc/pam.d/elogind-user
EOF
}

sha256sums=('941fde1ffbdf51d61e47fcebc49e2fc2b1347fcf3b0522bfa9d65ad5da653e53')
