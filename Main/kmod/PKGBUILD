# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=kmod
pkgver=27
pkgrel=1
pkgdesc="The Kmod package contains libraries and utilities for loading kernel modules." 
arch=('x86_64')
url="http://www.kernel.org/"
license=('GPL3')
groups=('core')
depends=('glibc')
makedepends=('bash'
             'binutils'
             'bison'
             'coreutils'
             'flex'
             'gcc'
             'gettext'
             'gzip'
             'make'
             'sed'
             'xz'
             'zlib')
source=("https://www.kernel.org/pub/linux/utils/kernel/${pkgname}/${pkgname}-${pkgver}.tar.xz")        
options=('debug' 'strip')

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr          \
              --bindir=/bin          \
              --sysconfdir=/etc      \
              --with-rootlibdir=/lib \
              --with-xz              \
              --with-zlib
  make
}

#check(){
#  cd "${pkgname}-${pkgver}"
#  make check
#}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/sbin"
  for target in depmod insmod lsmod modinfo modprobe rmmod; do
    ln -sfv ../bin/kmod "${pkgdir}/sbin/${target}"
  done
  ln -sfv kmod "${pkgdir}/bin/lsmod"
}
sha256sums=('c1d3fbf16ca24b95f334c1de1b46f17bbe5a10b0e81e72668bdc922ebffbbc0c')
