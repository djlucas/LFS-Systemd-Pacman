# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=kmod
pkgver=27
pkgrel=1
pkgdesc="The Kmod package contains libraries and utilities for loading kernel modules." 
arch=('x86_64')
url="http://www.kernel.org/"
license=('GPL3')
groups=('core')
depends=('glibc')
makedepends=('bash'
             'binutils'
             'bison'
             'coreutils'
             'flex'
             'gcc'
             'gettext'
             'gzip'
             'make'
             'sed'
             'xz'
             'zlib')
source=("https://www.kernel.org/pub/linux/utils/kernel/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        "depmod.hook"
        "depmod.script") 

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr          \
              --bindir=/bin          \
              --sysconfdir=/etc      \
              --with-rootlibdir=/lib \
              --with-xz              \
              --with-zlib
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/sbin"
  for target in depmod insmod lsmod modinfo modprobe rmmod; do
    ln -sfv ../bin/kmod "${pkgdir}/sbin/${target}"
  done
  ln -sfv kmod "${pkgdir}/bin/lsmod"
  # Install pacman hooks for module handling
  install -vdm755 "${pkgdir}"/usr/share/libalpm/{hooks,scripts}
  install -vm644 "${srcdir}/depmod.hook" "${pkgdir}/usr/share/libalpm/hooks"
  install -vm755 "${srcdir}/depmod.script" \
                 "${pkgdir}/usr/share/libalpm/scripts/depmod"
}
sha256sums=('c1d3fbf16ca24b95f334c1de1b46f17bbe5a10b0e81e72668bdc922ebffbbc0c'
            '125cf6f2eb7b5c80fb5b2e5f590eb49fd195ed877728cdd1fa46067a724b80d2'
            '827c02e868777e8fcaa91fd519966ef5d30c8d30bad1b20eb56e01cbc9a2e43f')
