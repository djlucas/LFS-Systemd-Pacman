# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=libffi
pkgver=3.3
pkgrel=1
pkgdesc='Portable foreign function interface library'
arch=('x86_64')
url='https://sourceware.org/libffi/'
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
source=(https://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz)
options=('debug' 'strip')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr          \
              --disable-static       \
              --enable-pax_emutramp  \
              --with-gcc-arch=x86_64
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
}
sha256sums=('72fba7922703ddfa7a028d513ac15a85c8d54c8d67f55fa5a4802885dc652056')
