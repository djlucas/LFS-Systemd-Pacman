# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

_pkgname=libXcomposite
pkgname=xorg-libXcomposite
pkgver=0.4.5
pkgrel=1
pkgdesc="libXcomposite contains the X.org composite extension library."
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
groups=('xorg')
depends=('glibc'
         'libxcb'
         'util-linux'
         'xorg-libX11'
         'xorg-libXau'
         'xorg-libXdmcp'
         'xorg-libXfixes')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gawk'
             'gcc'
             'gettext'
             'make'
             'xorg-util-macros'
             'xorg-xorgproto')
optdepends=('fop'
            'libxslt'
            'links'
            'lynx'
            'w3m'
            'xmlto')
source=("${url}/releases/individual/lib/${_pkgname}-${pkgver}.tar.bz2")

build(){
  cd "${_pkgname}-${pkgver}"
  ./configure --prefix=/usr        \
              --sysconfdir=/etc    \
              --localstatedir=/var \
              --disable-static     \
              --docdir="/usr/share/doc/${_pkgname}-${pkgver}"
  make
}

package(){
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

sha256sums=('b3218a2c15bab8035d16810df5b8251ffc7132ff3aa70651a1fba0bfe9634e8f')
