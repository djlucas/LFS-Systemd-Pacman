# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=autoconf213
_pkgname=autoconf
pkgver=2.13
pkgrel=1
pkgdesc="The Autoconf package contains programs for producing shell scripts that can automatically configure source code."
arch=('any')
url="http://www.gnu.org/software/${_pkgname}/"
groups=('core')
license=('GPL2' 'GPL3' 'custom')
makedepends=('bash'
             'coreutils'
             'diffutils'
             'grep'
             'm4'
             'make'
             'perl'
             'sed'
             'texinfo')
depends=('gawk'
         'm4'
         'diffutils'
         'bash')
source=("http://ftp.gnu.org/gnu/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "http://www.linuxfromscratch.org/patches/blfs/svn/${_pkgname}-${pkgver}-consolidated_fixes-1.patch")

prepare(){
  cd "${_pkgname}-${pkgver}"
  patch -Np1 -i ../autoconf-2.13-consolidated_fixes-1.patch
  mv -v autoconf.texi autoconf213.texi
  rm -v autoconf.info
}

build(){
  cd "${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --program-suffix=2.13
  make
}

check(){
  cd "${_pkgname}-${pkgver}"
  make check
}

package(){
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vm644 autoconf213.info "${pkgdir}/usr/share/info"
}
sha256sums=('f0611136bee505811e9ca11ca7ac188ef5323a8e2ef19cffd3edb3cf08fd791e'
            '4c1d143eb5be8f3c9d0dffded238d3ca9080cf7e041cd1c97e7bf5adb5d20ef8')

