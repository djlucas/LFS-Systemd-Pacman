# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=libxslt
pkgver=1.1.34
pkgrel=1
pkgdesc="The libxslt package contains XSLT libraries used for extending libxml2 libraries to support XSLT files." 
arch=('x86_64')
url="http://www.xmlsoft.org/"
license=('GPL3')
groups=('core')
depends=('libxml2')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gcc'
             'make'
             'sed')
source=("http://xmlsoft.org/sources/${pkgname}-${pkgver}.tar.gz")

prepare(){
  cd "${pkgname}-${pkgver}"
  sed -i s/3000/5000/ libxslt/transform.c doc/xsltproc.{1,xml}
}

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr    \
              --disable-static \
              --without-python
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  sed -e 's@http://cdn.docbook.org/release/xsl@https://cdn.docbook.org/release/xsl-nons@' \
      -e 's@\$Date\$@31 October 2019@' -i doc/xsltproc.xml &&
  xsltproc/xsltproc --nonet doc/xsltproc.xml -o doc/xsltproc.1
  make DESTDIR="${pkgdir}" install
}

sha256sums=('98b1bd46d6792925ad2dfe9a87452ea2adebf69dcb9919ffd55bf926a7f93f7f')
