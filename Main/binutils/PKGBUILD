# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=binutils
pkgver=2.35.1
pkgrel=1
arch=('x86_64')
pkgdesc="The Binutils package contains a linker, an assembler, and other tools for handling object files."
url="http://www.gnu.org/software/${pkgname}/"
license=('GPL')
groups=('core')
depends=('glibc'
         'zlib')
makedepends=('bash'
             'binutils'
             'coreutils'
             'diffutils'
             'file'
             'gawk'
             'gcc'
             'grep'
             'make'
             'perl'
             'sed'
             'texinfo')
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz")
options=('staticlibs')

prepare(){
  cd "${pkgname}-${pkgver}"

  # Turn off development mode (-Werror, gas run-time checks, date in sonames)
  sed -i '/^development=/s/true/false/' bfd/development.sh

  # Kill $CPP and $CPPFLAGS when testing header files (Thanks Arch Linux)
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  # Kill off a broken test
  sed -i '/@\tincremental_copy/d' gold/testsuite/Makefile.in

  mkdir -v build
}

build(){ 
  cd "${pkgname}-${pkgver}/build"
  ../configure --prefix=/usr       \
               --enable-gold       \
               --enable-ld=default \
               --enable-plugins    \
               --enable-shared     \
               --disable-werror    \
               --enable-64-bit-bfd \
               --with-system-zlib  \
               --enable-deterministic-archives

  make tooldir=/usr
}

check(){
  cd "${pkgname}-${pkgver}/build"
  make LDFLAGS="" -k check 2>&1 | tee test.log || true
  # one test fails and four error with new kenerl headers
  # nine fail if in chroot
  failures=`grep "tests failed" test.log | cut -d " " -f 1`
  if test "${failures}" -gt "9"; then
    echo "Unexpected test failures..." && exit 1
  fi
}

package(){
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" tooldir=/usr install
}

sha256sums=('3ced91db9bf01182b7e420eab68039f2083aed0a214c0424e257eae3ddee8607')
