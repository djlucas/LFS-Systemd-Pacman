# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=iproute2
pkgver=5.9.0
pkgrel=1
pkgdesc="The IPRoute2 package contains programs for basic and advanced IP networking."
arch=('x86_64')
url="https://www.kernel.org/pub/linux/utils/net/${pkgname}/"
license=('GPL3')
groups=('core')
depends=('glibc')
makedepends=('bash'
             'bison'
             'coreutils'
             'flex'
             'gcc'
             'make'
             'libcap'
             'libelf'
             'linux-api-headers')
source=("https://www.kernel.org/pub/linux/utils/net/${pkgname}/${pkgname}-${pkgver}.tar.xz")

prepare() {
  cd "${pkgname}-${pkgver}"
  sed -i /ARPD/d Makefile
  rm -fv man/man8/arpd.8
  sed -i 's/.m_ipt.o//' tc/Makefile
}

build(){
  cd "${pkgname}-${pkgver}"
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" DOCDIR="/usr/share/doc/${pkgname}-${pkgver}" install
}

sha256sums=('a25dac94bcdcf2f73316c7f812115ea7a5710580bad892b08a83d00c6b33dacf')
