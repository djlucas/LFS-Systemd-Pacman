# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=m4
pkgver=1.4.18
pkgrel=1
pkgdesc="The M4 package contains a macro processor." 
arch=('x86_64')
url="http://www.gnu.org/software/m4/"
license=('GPL3')
groups=('core')
depends=('bash'
         'glibc')
makedepends=('binutils'
             'coreutils'
             'gcc'
             'glibc'
             'grep'
             'make'
             'sed'
             'texinfo')
source=("https://ftp.gnu.org/gnu/m4/${pkgname}-${pkgver}.tar.xz")        

prepare() {
  cd "${pkgname}-${pkgver}"
  sed -i 's/IO_ftrylockfile/IO_EOF_SEEN/' lib/*.c
  echo "#define _IO_IN_BACKUP 0x100" >> lib/stdio-impl.h
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

sha256sums=('f2c1e86ca0a404ff281631bdc8377638992744b175afb806e25871a24a934e07')
