# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=cracklib
pkgver=2.9.7
pkgrel=1
pkgdesc="The CrackLib package contains a library used to enforce strong passwords by comparing user selected passwords to words in chosen word lists." 
arch=('x86_64')
url="http://www.gnu.org/software/cracklib"
license=('GPL3')
groups=('core')
depends=('glibc')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gcc'
             'grep'
             'make')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        "https://github.com/${pkgname}/${pkgname}/releases/download/v${pkgver}/${pkgname}-words-${pkgver}.bz2")        
install="cracklib.install"
backup=("usr/share/dict/cracklib-extra-words")
options=('!emptydirs')

build(){
  cd "${pkgname}-${pkgver}"
  sed -i '/skipping/d' util/packer.c
  ./configure --prefix=/usr    \
              --disable-static \
              --with-default-dict=/lib/cracklib/pw_dict
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/lib"
  mv -v "${pkgdir}"/usr/lib/libcrack.so.* "${pkgdir}/lib"
  ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libcrack.so") \
          "${pkgdir}/usr/lib/libcrack.so"
  install -v -m644 -D "${srcdir}/${pkgname}-words-${pkgver}.bz2" \
                      "${pkgdir}/usr/share/dict/${pkgname}-words.bz2"
  bzip2 -d -v "${pkgdir}/usr/share/dict/cracklib-words.bz2"
  ln -v -sf cracklib-words "${pkgdir}/usr/share/dict/words"
  echo "linuxfromscratch" >> "${pkgdir}/usr/share/dict/cracklib-extra-words"
  install -v -m755 -d "${pkgdir}/lib/cracklib"
}

sha256sums=('fe82098509e4d60377b998662facf058dc405864a8947956718857dbb4bc35e6'
            'ec25ac4a474588c58d901715512d8902b276542b27b8dd197e9c2ad373739ec4')
