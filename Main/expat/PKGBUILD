# Maintainer:  DJ Lucas <dj@linuxfromscratch.org>

pkgname=expat
pkgver=2.2.10
pkgrel=1
pkgdesc="The Expat package contains a stream oriented C library for parsing XML."
arch=('x86_64')
url="http://expat.sourceforge.net/"
license=('custom')
groups=('core')
depends=('glibc')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gawk'
             'gcc'
             'grep'
             'make'
             'sed')
optdepends=('docbook2x')
source=("http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
_confargs=""

build() {
  cd "${pkgname}-${pkgver}"
  if ! $(pacman -Q docbook2x > /dev/null 2>&1); then
    _confargs="${_confargs} --without-docbook"
  fi
  ./configure --prefix=/usr    \
              --disable-static \
              ${_confargs}     \
              --docdir=/usr/share/doc/"${pkgname}-${pkgver}"
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  install  -vm644 doc/*.{html,png,css} \
                  "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
}

sha256sums=('b2c160f1b60e92da69de8e12333096aeb0c3bf692d41c60794de278af72135a5')

