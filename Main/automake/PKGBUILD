# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=automake
pkgver=1.16.2
pkgrel=1
pkgdesc="The Automake package contains programs for generating Makefiles for use with Autoconf."
arch=('any')
url="http://www.gnu.org/software/${pkgname}/"
groups=('core')
license=('GPL')
makedepends=('bash'
             'coreutils'
             'gettext'
             'grep'
             'm4'
             'make'
             'perl'
             'sed'
             'texinfo')
depends=('autoconf')
checkdepends=('gcc'
              'libtool')
optdepends=('dejagnu: additional test suite coverage'
            'python: additional test suite coverage'
            'gcc-fortran: additional test suite coverage'
            'jdk: additional test suite coverage'
            'vala: additional test suite coverage'
            'emacs: additional test suite coverage'
            'cscope: additional test suite coverage')
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz")

prepare(){
  cd "${pkgname}-${pkgver}"
  sed -i "s/''/etags/" t/tags-lisp-space.sh
}

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --docdir="/usr/share/doc/${pkgname}-${pkgver}"
  make
}

check(){
  cd "${pkgname}-${pkgver}"
  make -j4 check
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

sha256sums=('ccc459de3d710e066ab9e12d2f119bd164a08c9341ca24ba22c9adaa179eedd0')
