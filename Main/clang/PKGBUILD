# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=clang
pkgver=11.0.0
pkgrel=1
pkgdesc="The LLVM package contains a collection of modular and reusable compiler and toolchain technologies."
arch=('x86_64')
url="https://github.com/llvm/"
license=('GPL3')
groups=('core')
depends=('compiler-rt'
         'glibc'
         'llvm')
makedepends=('bash'
             'binutils'
             'cmake'
             'coreutils'
             'gcc'
             'grep'
             'make'
             'ninja'
             'sed')
optdepends=('doxygen'
            'git'
            'graphviz'
            'libxml2'
            'python2'
            'ocaml'
            'python2-pyyaml'
            'rsync'
            'python3-sphinx'
            'tex'
            'valgrind'
            'zip')
source=("https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/${pkgname}-${pkgver}.src.tar.xz"
        "https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/clang-tools-extra-$pkgver.src.tar.xz"
        "https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/llvm-${pkgver}.src.tar.xz")
prepare(){
  cd "${pkgname}-${pkgver}.src"
  mkdir -v build
  mv "${srcdir}/clang-tools-extra-${pkgver}.src" tools/extra
  sed '/add_clang_subdirectory(handle-llvm)/d' \
      -i tools/clang-fuzzer/CMakeLists.txt
}

build(){
  cd "${pkgname}-${pkgver}.src/build"
  _mflags=`echo "${MAKEFLAGS}" | grep -o -- "-j[ ]\{0,1\}[0-9]\{1,2\}"`
  CC=gcc CXX=g++                                  \
  cmake -DCMAKE_INSTALL_PREFIX=/usr               \
        -DLLVM_ENABLE_FFI=ON                      \
        -DCMAKE_BUILD_TYPE=Release                \
        -DLLVM_BUILD_LLVM_DYLIB=ON                \
        -DLLVM_LINK_LLVM_DYLIB=ON                 \
        -DLLVM_ENABLE_RTTI=ON                     \
        -DCLANG_LINK_CLANG_DYLIB=ON               \
        -DLLVM_BUILD_TESTS=ON                     \
        -DLLVM_EXTERNAL_LIT=/usr/bin/lit          \
        -DPYTHON_EXECUTABLE=/usr/bin/python3      \
        -DLLVM_MAIN_SRC_DIR="${srcdir}/llvm-${pkgver}.src" \
        -Wno-dev -G Ninja ..
  ninja -j 4 #${_mflags}
}

check(){
  cd "${pkgname}-${pkgver}.src/build"
  _mflags=`echo "${MAKEFLAGS}" | grep -o -- "-j[ ]\{0,1\}[0-9]\{1,2\}"`
  ninja ${_mflags} check-clang{,-tools} || true
  # six tests are known to fail
}

package(){
  cd "${pkgname}-${pkgver}.src/build"
  DESTDIR="${pkgdir}" ninja install
}

sha256sums=('0f96acace1e8326b39f220ba19e055ba99b0ab21c2475042dbc6a482649c5209'
            'fed318f75d560d0e0ae728e2fb8abce71e9d0c60dd120c9baac118522ce76c09'
            '913f68c898dfb4a03b397c5e11c6a2f39d0f22ed7665c9cefa87a34423a72469')
