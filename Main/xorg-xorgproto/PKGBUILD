# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

_pkgname=xorgproto
pkgname=xorg-xorgproto
pkgver=2020.1
pkgrel=1
pkgdesc="The xorgproto package provides the header files required to build the X Window system, and to allow other applications to build against the installed X Window system."
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
groups=('xorg')
depends=('glibc')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gcc'
             'meson'
             'ninja'
             'xorg-util-macros')
optdepends=('asciidoc'
            'fop'
            'libxslt'
            'xmlto')
source=("${url}/releases/individual/proto/${_pkgname}-${pkgver}.tar.bz2")

build(){
  cd "${_pkgname}-${pkgver}"
  mkdir build
  cd build
  meson --prefix=/usr -Dlegacy=true ..
  ninja
}

package(){
  cd "${_pkgname}-${pkgver}/build"
  DESTDIR="${pkgdir}" ninja install
  install -vdm755 "${pkgdir}/usr/share/doc/${_pkgname}-${pkgver}"
  install -vm644  ../[^m]*.txt ../PM_spec \
                  "${pkgdir}/usr/share/doc/${_pkgname}-${pkgver}"
  # Apple WM protocols are not appropriate on Linux
  rm -f "${pkgdir}"/usr/include/X11/extensions/apple*
  rm -f "${pkgdir}"/usr/share/pkgconfig/applewmproto.pc
}

sha256sums=('54a153f139035a376c075845dd058049177212da94d7a9707cf9468367b699d2')
