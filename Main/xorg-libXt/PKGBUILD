# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

_pkgname=libXt
pkgname=xorg-libXt
pkgver=1.2.0
pkgrel=2
pkgdesc="libXt contains the X.org toolkit library."
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
groups=('xorg')
depends=('glibc'
         'libxcb'
         'util-linux'
         'xorg-libICE'
         'xorg-libSM'
         'xorg-libX11'
         'xorg-libXau'
         'xorg-libXdmcp')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gcc'
             'make'
             'xorg-libXScrnSaver'
             'xorg-util-macros'
             'xorg-xorgproto')
optdepends=('fop'
            'libxslt'
            'links'
            'lynx'
            'w3m'
            'xmlto')
source=("${url}/releases/individual/lib/${_pkgname}-${pkgver}.tar.bz2")

build(){
  cd "${_pkgname}-${pkgver}"
  ./configure --prefix=/usr        \
              --sysconfdir=/etc    \
              --localstatedir=/var \
              --disable-static     \
              --docdir="/usr/share/doc/${_pkgname}-${pkgver}" \
              --with-appdefaultdir=/etc/X11/app-defaults
  make
}

package(){
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

sha256sums=('b31df531dabed9f4611fc8980bc51d7782967e2aff44c4105251a1acb5a77831')
