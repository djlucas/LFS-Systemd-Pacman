# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=gmp
pkgver=6.2.1
pkgrel=2
arch=('x86_64')
url="http://gmplib.org/"
license=('LGPL3' 'GPL')
groups=('core')
depends=('glibc')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gawk'
             'grep'
             'gcc'
             'sed')
source=("https://gmplib.org/download/gmp/gmp-${pkgver}.tar.xz")

build() {
  cd "${pkgname}-${pkgver}"

  cp -v configfsf.guess config.guess
  cp -v configfsf.sub   config.sub

  ./configure --prefix=/usr    \
              --enable-cxx     \
              --disable-static \
              --docdir="/usr/share/doc/${pkgname}-${pkgver}"
  make
  make html
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-html
}

sha256sums=('fd4829912cddd12f84181c3451cc752be224643e87fac497b69edddadc49b4f2')
