# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=gmp
pkgver=6.2.0
pkgrel=1
arch=('x86_64')
url="http://gmplib.org/"
license=('LGPL3' 'GPL')
source=("https://gmplib.org/download/gmp/gmp-${pkgver}.tar.xz")

build() {
  cd "${pkgname}-${pkgver}"

  cp -v configfsf.guess config.guess
  cp -v configfsf.sub   config.sub

  ./configure --prefix=/usr    \
              --enable-cxx     \
              --disable-static \
              --docdir="/usr/share/doc/${pkgname}-${pkgver}"
  make
  make html
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-html
}

sha256sums=('258e6cd51b3fbdfc185c716d55f82c08aff57df0c6fbd143cf6ed561267a1526')
