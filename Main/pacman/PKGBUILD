# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=pacman
pkgver=5.2.2
pkgrel=3
pkgdesc="Pacman is a library-based package manager with dependency support."
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('core')
depends=('bash'
         'curl'
         'glibc'
         'gpgme'
         'libarchive')
makedepends=('asciidoc'
             'libxslt')
#checkdepends=('python3'
#              'fakeroot')
backup=('etc/pacman.conf'
        'etc/makepkg.conf')
source=("https://sources.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "makepkg.conf"
        "pacman.conf"
        "http://www.linuxfromscratch.org/~dj/${pkgname}-${pkgver}-no_debug_src-2.patch")

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/${pkgname}-${pkgver}-no_debug_src-2.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr        \
              --sysconfdir=/etc    \
              --localstatedir=/var \
              --enable-doc         \
              --with-scriptlet-shell=/bin/bash \
              --with-ldconfig=/sbin/ldconfig
  make
}

#check() {
#  cd "${pkgname}-${pkgver}"
#  make check
#}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/etc"
  install -vm644  "${srcdir}/pacman.conf" "${pkgdir}/etc/pacman.conf"
  install -vm644  "${srcdir}/makepkg.conf" "${pkgdir}/etc/makepkg.conf"
}

sha256sums=('bb201a9f2fb53c28d011f661d50028efce6eef2c1d2a36728bdd0130189349a0'
            '9f16591284066460ce0eee253be549ed4962489fd1e53e7e10401f3816e8e71c'
            'a8fa9ac9746481ab5fa9ff01cd82bcf6c7d9c2e5e5648340757529243a03e286'
            '181562fba675fa4cd97ff045b8bdf264f4dba2129afd4b3fa8b5f87ecf67d3f0')

