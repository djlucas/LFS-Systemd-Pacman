# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=sysklogd
pkgver=1.5.1
pkgrel=1
pkgdesc="The sysklogd package contains programs for logging system messages, such as those given by the kernel when unusual things happen." 
arch=('x86_64')
url="http://www.infodrom.org/projects/sysklogd/"
license=('GPL3')
groups=('core')
depends=('glibc')
makedepends=('binutils'
             'coreutils'
             'gcc'
             'make'
             'patch')
backup=('etc/syslog.conf')
source=("http://www.infodrom.org/projects/${pkgname}/download/${pkgname}-${pkgver}.tar.gz")

prepare(){
  cd "${pkgname}-${pkgver}"
  sed -i '/Error loading kernel symbols/{n;n;d}' ksym_mod.c
  sed -i 's/union wait/int/' syslogd.c
}

build(){
  cd "${pkgname}-${pkgver}"
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" BINDIR="/sbin"install
  install -vdm755 "${pkgdir}/etc"
  cat > "${pkgdir}/etc/syslog.conf" << "EOF"
# Begin /etc/syslog.conf

auth,authpriv.* -/var/log/auth.log
*.*;auth,authpriv.none -/var/log/sys.log
daemon.* -/var/log/daemon.log
kern.* -/var/log/kern.log
mail.* -/var/log/mail.log
user.* -/var/log/user.log
*.emerg *

# End /etc/syslog.conf
EOF
}

sha256sums=('5166c185ae23c92e8b9feee66a6e3d0bc944bf673112f53e3ecf62e08ce7c201')
