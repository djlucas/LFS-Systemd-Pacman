# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=tzdata
_ver=2020b
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgrel=1
pkgdesc="The Time Zone Database (often called tz or zoneinfo) contains code and data that represent the history of local time for many representative locations around the globe."
arch=('x86_64')
url="http://www.iana.org/time-zones/"
license=('GPL')
groups=('core')
options=('!debug' '!strip')
makedepends=('bash'
             'coreutils'
             'grep'
             'make')
depends=('glibc')
source=("https://www.iana.org/time-zones/repository/releases/${pkgname}${_ver}.tar.gz")

package(){
  cd "${srcdir}"
  ZONEINFO="${pkgdir}/usr/share/zoneinfo"
  install -vdm755 "${ZONEINFO}"/{posix,right}

  for tz in etcetera southamerica northamerica europe africa antarctica  \
          asia australasia backward; do
    /usr/sbin/zic -L /dev/null   -d "${ZONEINFO}"       "${tz}"
    /usr/sbin/zic -L /dev/null   -d "${ZONEINFO}/posix" "${tz}"
    /usr/sbin/zic -L leapseconds -d "${ZONEINFO}/right" "${tz}"
  done

  cp -v zone.tab zone1970.tab iso3166.tab "${ZONEINFO}"
  /usr/sbin/zic -d "${ZONEINFO}" -p America/New_York
  unset ZONEINFO
}

sha256sums=('9b053f951d245ce89d850b96ee4711d82d833559b1fc96ba19f90bc4d745e809')
