# Maintainer: DJ Lucas <dj@linuxfromscratch.org>

pkgname=attr
pkgver=2.4.48
pkgrel=1
pkgdesc="The attr package contains utilities to administer the extended attributes on filesystem objects."
arch=('x86_64')
url="http://www.gnu.org/software/${pkgname}/"
groups=('core')
license=('LGPL')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gawk'
             'gcc'
             'gettext'
             'grep'
             'make'
             'sed'
             'tar')
depends=('glibc')
source=("http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz")

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr     \
              --bindir=/bin     \
              --disable-static  \
              --sysconfdir=/etc \
              --docdir=/usr/share/doc/${pkgname}-${pkgver}
  make
}

check(){
  cd "${pkgname}-${pkgver}"
  make check
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/lib"
  mv -v ${pkgdir}/usr/lib/libattr.so.* ${pkgdir}/lib
  ln -sfv ../../lib/$(readlink "${pkgdir}/usr/lib/libattr.so") \
          "${pkgdir}/usr/lib/libattr.so"
}
sha256sums=('5ead72b358ec709ed00bbf7a9eaef1654baad937c001c044fe8b74c57f5324e7')

