# Maintainer: archetech trw2006@hotmail.com
pkgname=expect
pkgver=5.45.4
pkgrel=1
pkgdesc="Expect contains tools for automating interactive applications such as telnet, ftp, passwd, fsck, rlogin, tip, etc."
arch=('x86_64')
url="http://expect.sourceforge.net/"
groups=('core')
license=('GPL')
depends=('tcl' 'gcc' 'glibc')
optdepends=('tk')
makedepends=('bash' 'coreutils' 'diffutils' 'grep' 'make' 'patch' 'sed' )
source=("http://prdownloads.sourceforge.net/${pkgname}/${pkgname}${pkgver}.tar.gz")
options=(debug strip)

build() {
  cd "${pkgname}${pkgver}"
  ./configure --prefix=/usr           \
              --with-tcl=/usr/lib     \
              --enable-shared         \
              --mandir=/usr/share/man \
              --with-tclinclude=/usr/include &&
  make
}

check() {
  cd "${pkgname}${pkgver}"
  make test
}

package() {
  cd "${pkgname}${pkgver}"
  make DESTDIR="${pkgdir}" install &&
  ln -svf "${pkgname}${pkgver}/lib${pkgname}${pkgver}.so" "${pkgdir}/usr/lib"
}

sha256sums=('49a7da83b0bdd9f46d04a04deec19c7767bb9a323e40c4781f89caf760b92c34')
